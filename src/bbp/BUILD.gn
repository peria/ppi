import("//build/toolchain/gpu_toolchain.gni")

static_library("bbp") {
  sources = [
    "bbp.cc",
    "bbp.h",
  ]
  deps = [
    "//src/base",
    "//src/number",
    "//src/number:montgomery",
    "//third_party/glog",
  ]

  if (current_toolchain == gpu_toolchain) {
    sources += [
      "bbp_gpu.cc",
      "bbp_gpu.h",
    ]
  }
}

# ------------------------------------------

group("bbp_exec") {
  if (gpu_toolchain == "") {
    deps = [ ":bbp_main" ]
  } else {
    deps = [ ":bbp_main($gpu_toolchain)" ]
  }
}

executable("bbp_main") {
  sources = [ "main.cc" ]
  deps = [
    ":bbp",
    "//src/base",
    "//third_party/glog",
    "//third_party/gflags",
  ]
}

executable("bbp_test") {
  testonly = true
  sources = [ "bbp_test.cc" ]
  deps = [
    ":bbp",
    "//src/base",
    "//third_party/gtest",
    "//third_party/gtest:gtest_main",
  ]
}

