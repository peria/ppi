import("//build/toolchain/gpu_toolchain.gni")

static_library("bbp") {
  sources = [
     "bbp.cc",
     "bbp.h",
  ]
  deps = [
    "//src/base",
    "//src/number",
    "//src/number:montgomery"
  ]
}

# ------------------------------------------

group("bbp_exec") {
  deps = [
    ":bbp_main",
  ]
  if (gpu_toolchain != "") {
    deps += [ ":bbp_gpu($gpu_toolchain)" ]
  }
}

executable("bbp_main") {
  sources = [ "main.cc" ]
  deps = [
    ":bbp",
    "//src/base",
    "//third_party/glog",
    "//third_party/gflags",
  ]
}

if (current_toolchain == gpu_toolchain) {
  executable("bbp_gpu") {
    sources = [ "main.cc" ]
    deps = [
      ":bbp",
      "//src/base($default_toolchain)",
      "//third_party/glog($default_toolchain)",
      "//third_party/gflags($default_toolchain)",
    ]
  }
}

executable("bbp_test") {
  testonly = true
  sources = [ "bbp_test.cc" ]
  deps = [
    ":bbp",
    "//src/base",
    "//third_party/gtest",
    "//third_party/gtest:gtest_main",
  ]
}

