import("//src/number/number.gni")

static_library("number") {
  sources = [
    "natural.cc",
    "natural.h",
  ]
  deps = [
    ":internal",
    "//src/base",
    "//third_party/glog",
  ]
}

static_library("internal") {
  if (use_decimal_operation) {
    radix_dir = get_path_info("./decimal", "abspath")
  } else {
    radix_dir = get_path_info("./hex", "abspath")
  }

  sources = [
    "${radix_dir}/add.cc",
    "${radix_dir}/div.cc",
    "${radix_dir}/mult.cc",
    "${radix_dir}/ostream.cc",
    "${radix_dir}/subtract.cc",
    "internal.cc",
    "internal.h",
    "uint128.h",
  ]
  deps = [
    "//src/base",
    "//third_party/glog",
  ]
}

config("number_base") {
  if (use_decimal_operation) {
    defines = [
      "NUMBER_BASE=10",
    ]
  } else {
    defines = [
      "NUMBER_BASE=16",
    ]
  }
}

executable("number_test") {
  testonly = true
  sources = [
    "natural_test.cc",
  ]
  deps = [
    ":number",
    "//src/base",
    "//third_party/gtest",
    "//third_party/gtest:gtest_main",
  ]
  configs += [
    ":number_base"
  ]
}

executable("number_benchmark") {
  testonly = true
  sources = [
    "natural_benchmark.cc",
  ]
  deps = [
    ":number",
    "//src/base",
    "//third_party/benchmark",
  ]
  configs += [
    ":number_base"
  ]
}
