declare_args() {
  os = host_os
}

is_posix = os == "mac" || os == "linux"
is_mac = os == "mac"
is_linux = os == "linux"
is_win = os == "win" || os == "winuwp"

declare_args() {
  # true if debug build.
  is_debug = true
  # true to enable ASan.
  is_asan = false
}

set_default_toolchain("//build/toolchain/$os:x64")

default_compiler_configs = [
  "//build/config/compiler",
  "//build/config:global",
]

if (is_debug) {
  default_compiler_configs += [
    "//build/config:debug",
    "//build/config/compiler:no_optimize",
  ]
} else {
  default_compiler_configs += [
    "//build/config:release",
    "//build/config/compiler:optimize",
  ]
}

if (is_asan) {
  default_compiler_configs += [
    "//build/config:asan",
  ]
}

set_defaults("executable") {
  configs = default_compiler_configs
}

set_defaults("static_library") {
  configs = default_compiler_configs
}

set_defaults("shared_library") {
  configs = default_compiler_configs
}

set_defaults("source_set") {
  configs = default_compiler_configs
}
